plugins {
    id "java-gradle-plugin"
    id "groovy"
    id "jacoco"
    id "com.gradle.plugin-publish" version "0.12.0"
    id "com.github.kt3k.coveralls" version "2.5.0"
}

apply from: 'gradle/funcTest.gradle'

group 'org.kt3k.gradle.plugin'
version '2.12.1'

repositories {
    jcenter()
}

dependencies {
    implementation localGroovy()
    implementation gradleApi()
    implementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    implementation 'org.apache.httpcomponents:httpmime:4.5.11'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.5.1.201910021850-r'
    testImplementation 'junit:junit:4.11'
    testImplementation 'org.mockito:mockito-all:1.9.5'
    testImplementation 'com.github.tomakehurst:wiremock:2.23.2'

    functionalTestImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

tasks.withType(JavaCompile) {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

tasks.withType(GroovyCompile) {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

gradlePlugin {
  testSourceSets sourceSets.functionalTest
  plugins {
    coverallsPlugin {
      id = 'com.github.kt3k.coveralls'
      implementationClass = 'org.kt3k.gradle.plugin.CoverallsPlugin'
    }
  }
}

pluginBundle {
  website = 'http://github.com/kt3k/coveralls-gradle-plugin/'
  vcsUrl = 'https://github.com/kt3k/coveralls-gradle-plugin.git'
  description = 'Send coverage data to coveralls.io'
  tags = ['coverage', 'coveralls']

  plugins {
    coverallsPlugin {
      displayName = 'Coveralls Gradle Plugin'
    }
  }
}

test {
    inputs.dir "$projectDir/src/test/fixture"
}
