// build.gradle

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'signing'
apply plugin: 'cobertura'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

dependencies {
    compile localGroovy()
    compile gradleApi()

    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    compile 'org.apache.httpcomponents:httpmime:4.3'

    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'com.github.tomakehurst:wiremock:1.18'
}

buildscript {
    repositories {

        mavenCentral()
        jcenter()

    }

    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.4'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}


task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from 'build/docs/groovydoc'
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives jar

    archives groovydocJar
    archives sourcesJar
}


// sign archives if signatory exists
if (signing.signatory) {

    signing {
        sign configurations.archives
    }

}


uploadArchives {
    repositories {
        mavenDeployer {

            // sign pom if signatory exists
            if (signing.signatory) {
                beforeDeployment {
                    signing.signPom(it)
                }
            }

            repository(url: System.getenv('MVN_REPO')) {

                // set authentication info if sonatypeUsername property is set
                if (project.hasProperty('sonatypeUsername')) {
                    authentication userName: sonatypeUsername, password: sonatypePassword
                }
            }

            // additional pom settings (for maven central requirement)
            pom.project {

                name 'Gradle Coveralls Plugin'
                description 'This plugin helps to upload coverage data to Coveralls'
                url 'https://github.com/kt3k/coveralls-gradle-plugin'

                scm {
                    url 'scm:git@github.com:kt3k/coveralls-gradle-plugin.git'
                    connection 'scm:git@github.com:kt3k/coveralls-gradle-plugin.git'
                    developerConnection 'scm:git@github.com:kt3k/coveralls-gradle-plugin.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://raw.github.com/kt3k/coveralls-gradle-plugin/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'kt3k'
                        name 'Yoshiya Hinosawa'
                    }
                }
            }
        }
    }
}

cobertura.coverageFormats = ['html', 'xml']
cobertura.coverageSourceDirs = sourceSets.main.groovy.srcDirs


// MavenPublication settings
publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
        }
    }
}


bintray {

    if (project.hasProperty('bintrayUsername')) {
        // these two should be defined out of the repository, for example, in ~/.gradle/gradle.properties
        user = bintrayUsername
        key = bintrayApiKey
    }

    publications = ['plugin']

    pkg {
        repo = 'gradle-plugins'
        name = 'coveralls-gradle-plugin'
        desc = 'Send coverage data to coveralls.io.'
        websiteUrl = 'https://github.com/kt3k/coveralls-gradle-plugin'
        issueTrackerUrl = 'https://github.com/kt3k/coveralls-gradle-plugin/issues'
        vcsUrl = 'https://github.com/kt3k/coveralls-gradle-plugin.git'
        licenses = ['MIT']
        labels = ['coveralls', 'coverage']
        publicDownloadNumbers = true
        version {
            name = '1.0.2'
            vcsTag = 'v1.0.2'
            attributes = [
                'gradle-plugin': 'com.github.kt3k.coveralls:org.kt3k.gradle.plugin:coveralls-gradle-plugin'
            ]
        }
    }

}
